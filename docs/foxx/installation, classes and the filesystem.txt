"Import foxx (and the DOM addon) with the 'importScripts' function";
await importScripts('nexus-webdev/Ruin/addons/foxx.$', '~/~/~/foxx@dom.$');
foxx.config.maxDepth = 8; "Changes the maximum nesting depth of the program";

## 
# The 'install' function. This is only available when 'foxx@dom' is installed (choose your own folders);
-install RuinPrograms ? TG: Testing Ground;
-cd 'Testing Ground'; # changes the working directory;

# Writing to a file (so at least 1 .ari file exists). 1st expr: 10 (6 +4), 2nd: 5 (10 /2), 3rd: 25 (5 **2);
-write 'test.ari' >> \`6 4 +
 2 /
 2 ** \`;

# This program will create a simple expression evaluator (a very bad one by the way);
const results = Evaluator:run 'test.ari';
print results;

# Multi-Step Calculations must be done on separate lines.
  The Evaluator also disregards BODMAS.
  This interpreter is highly stupid and I cannot be bothered to fix it;

class Evaluator:static {
 def init() {
  # 's' acts as the global 'this' variable in all classes;
  s.stacks = {};
 };
 
 def run(filename = 'test.ari') {
  filename = filename.trim() +(filename.trim().endsWith('.ari') ? '' : '.ari');
  const stack = [];
  
  # Read the .ari (arithmetic) file targeted and split it into statements;
  const program = -read filename;
  const statements = program.split('\\n')
                            .filter(st => st.trim() != '');
  
  # Loop through all statements in the program and evaluate them;
  for st in statements ? {
   eval_expr st, stack;
  };
  
  s.stacks[filename] = stack;
  return stack;
 };
 
 function eval_expr(st, stack) {
  # Split the statement into tokens;
  const tokens = st.split(' ');
  
  for token in tokens ? {
   # Handle mathematical operations;
   if token == '+' ? {
    const b = stack.pop();
    const a = stack.pop();
    
    stack.push(a +b);
   };
   
   elif token == '-' ? {
    const b = stack.pop();
    const a = stack.pop();
    
    stack.push(a -b);
   };
   
   elif token == '*' ? {
    const b = stack.pop();
    const a = stack.pop();
    
    stack.push(a *b);
   };
   
   elif token == '/' ? {
    const b = stack.pop();
    const a = stack.pop();
    
    stack.push(a /b);
   };
   
   elif token == '**' ? {
    const b = stack.pop();
    const a = stack.pop();
    
    stack.push(a **b);
   };
   
   elif Number(token) ? {
    stack.push(Number(token));
   };
  };
 };
};

-rm 'test.ari';
!;